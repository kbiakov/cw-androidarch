apply plugin: 'com.android.application'

def keystorePropertiesFile = file('keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdkVersion 27
  buildToolsVersion "27.0.3"

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 27
    applicationId 'com.commonsware.android.mvi.paged'
    versionCode 1
    versionName "1.0"
    testApplicationId "com.commonsware.android.mvi.paged.test"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }
  }

  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
    release {
      shrinkResources true
      minifyEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  dataBinding {
    enabled = true
  }

  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }
}

def supportVer="27.0.2"
def lifecycleVer="1.1.0"
def roomVer="1.0.0"
def autoValueVer="1.5.1"
def retroVer="2.3.0"
def canaryVer="1.5.4"

dependencies {
  implementation        "com.commonsware.cwac:crossport:0.2.1"
  implementation        "com.commonsware.cwac:provider:0.5.3"
  implementation        "com.android.support:recyclerview-v7:$supportVer"
  implementation        "com.android.support:support-fragment:$supportVer"
  implementation        ("com.android.support:support-v4:$supportVer") {
    exclude group: 'com.android.support', module: 'support-media-compat'
  } // for https://issuetracker.google.com/issues/64909326
  implementation        "com.android.support.constraint:constraint-layout:1.0.2"
  implementation        "io.reactivex.rxjava2:rxjava:2.1.7"
  implementation        "io.reactivex.rxjava2:rxandroid:2.0.1"
  implementation        "android.arch.persistence.room:runtime:$roomVer"
  implementation        "android.arch.persistence.room:rxjava2:$roomVer"
  annotationProcessor   "android.arch.persistence.room:compiler:$roomVer"
  implementation        "android.arch.lifecycle:livedata:$lifecycleVer"
  implementation        "android.arch.lifecycle:viewmodel:$lifecycleVer"
  implementation        "android.arch.lifecycle:reactivestreams:$lifecycleVer"
  implementation        "android.arch.paging:runtime:1.0.0-alpha5"
  compileOnly           "com.google.auto.value:auto-value:$autoValueVer"
  annotationProcessor   "com.google.auto.value:auto-value:$autoValueVer"
  implementation        "com.samskivert:jmustache:1.13"
  implementation        "com.squareup.retrofit2:converter-gson:$retroVer"
  implementation        "com.squareup.retrofit2:adapter-rxjava2:$retroVer"
  debugImplementation   "com.squareup.leakcanary:leakcanary-android:$canaryVer"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$canaryVer"

  androidTestImplementation "com.android.support:support-annotations:27.0.2"
  androidTestImplementation "com.android.support.test:rules:1.0.1"
  androidTestImplementation "org.hamcrest:hamcrest-library:1.3"
  androidTestImplementation "org.mockito:mockito-android:2.10.0"
}